plugins {
  id 'net.saliman.properties' version '1.5.2'
  id "application"
  id "jacoco"
  id "org.sonarqube" version "4.4.1.3373"
  id 'com.github.johnrengelman.shadow' version '8.1.1'
}

repositories {
  mavenCentral()
  mavenLocal()
}

configurations {
  // Defines only those dependencies that we want to include in the assembly/shadow jar that will be used by spark-submit.
  shadowDependencies
}

dependencies {
  implementation "org.apache.spark:spark-sql_2.12:3.4.1"
  implementation "org.jcommander:jcommander:1.83"
  implementation "com.marklogic:marklogic-spark-connector:2.2-SNAPSHOT"

  implementation "org.apache.hadoop:hadoop-aws:3.3.6"
  implementation "org.apache.hadoop:hadoop-client:3.3.6"

  testImplementation "com.marklogic:marklogic-junit5:1.4.0"

  // Used for tests involving JDBC. Spring JDBC greatly simplifies executing SQL queries.
  testImplementation "org.springframework:spring-jdbc:5.3.31"
  testImplementation "org.postgresql:postgresql:42.6.0"

  shadowDependencies "org.jcommander:jcommander:1.83"
  shadowDependencies "com.marklogic:marklogic-spark-connector:2.2-SNAPSHOT"
}

application {
  mainClass = "com.marklogic.newtool.Main"
  // Removes warnings due to Spark performing "illegal reflective access".
  applicationDefaultJvmArgs = ['--add-opens', 'java.base/java.nio=ALL-UNNAMED', '--add-opens', 'java.base/java.net=ALL-UNNAMED',
                               '--add-opens', 'java.base/java.lang=ALL-UNNAMED', '--add-opens', 'java.base/java.util=ALL-UNNAMED',
                               '--add-opens', 'java.base/java.util.concurrent=ALL-UNNAMED']
}

// Modifies the application's start script to use our modified one that adds jars in the "./ext" folder to the classpath.
startScripts {
  unixStartScriptGenerator.template = resources.text.fromFile('scripts/start-script.txt')
  applicationName = "nt"
}

tasks.register("deleteTool", Delete) {
  delete "../nt"
}
tasks.register("buildTool", Copy) {
  from layout.buildDirectory.dir("install/new-tool-cli")
  into "../nt"
}
buildTool.dependsOn installDist, deleteTool

test {
  finalizedBy jacocoTestReport
}

jacocoTestReport {
  dependsOn test
  reports {
    xml.required = true
  }
}

sonar {
  properties {
    property "sonar.projectKey", "spark-etl"
    property "sonar.host.url", "http://localhost:9000"
  }
}

// See https://imperceptiblethoughts.com/shadow/configuration/dependencies/#embedding-jar-files-inside-your-shadow-jar .
shadowJar {
  configurations = [project.configurations.shadowDependencies]
}
